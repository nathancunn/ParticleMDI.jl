## Define a particle consisting of all cluster variables for a Categorical mixture
mutable struct CategoricalCluster
  n::Int                          # count of cluster members
  counts::Matrix{Int}     # count of occurrence of levels
  nlevels::Array{Float64}           # The denominator in logprob calculation
  CategoricalCluster(dataFile) =
  new(0,
      zeros(Int, maximum(Int, dataFile), size(dataFile, 2)),
      # [0.5 * maximum(dataFile[:, d]) for d = 1:size(dataFile, 2)]
      0.5 * maximum(dataFile, dims = 1))
end

function copy_particle1(particle::CategoricalCluster, dataFile)
    new_particle = CategoricalCluster(dataFile)
    new_particle.n = particle.n
    for i in eachindex(particle.counts)
      new_particle.counts[i] = particle.counts[i]
    end
    for i in eachindex(particle.nlevels)
      new_particle.nlevels[i] = particle.nlevels[i]
    end
    return new_particle
end

function copy_particle(particle::CategoricalCluster, dataFile)
    return deepcopy(particle)
end

function calc_logprob(obs, cl::CategoricalCluster, featureFlag::Array)
  out = - sum(log.(cl.nlevels[featureFlag] .+ cl.n))
  @inbounds for q = 1:length(obs)
    if featureFlag[q]
      if cl.n == 0
        out += log(0.5)
      else
        out += log(0.5 + cl.counts[obs[q], q])
      end
    end
  end
  return out
end

function cluster_add!(cl::CategoricalCluster, obs, featureFlag::Array)
  @inbounds cl.n  += Int(1)
  @simd for q = 1:length(obs)
    if featureFlag[q]
      @inbounds cl.counts[obs[q], q] += 1
    end
  end
  return
end

function calc_logmarginal(cl::CategoricalCluster)
  # This returns the log of the marginal likelihood of the cluster
  # Used for feature selection
  # Î²_0 = 0.5
  lm = zeros(Float64, length(cl.nlevels))
  for q in 1:length(cl.nlevels)
    lm[q] += SpecialFunctions.loggamma(cl.nlevels[q] * 2) - SpecialFunctions.loggamma.(cl.nlevels[q] * 2 .+ cl.n)
    for r in 1:Int(2 * cl.nlevels[q])
      lm[q] += SpecialFunctions.loggamma(cl.counts[r, q] + 0.5)

    end
  end
  return lm
end
using SpecialFunctions


"""
`coerce_categorical(data)`

Converts a matrix of categorical data into the format expected by particleMDI
## Input
- `data` an `n Ã— d` matrix containing discrete values of which relatively few are unique.


## Output
- An `n Ã— d` matrix where the discrete values are mapped to the range `1, 2, â€¦, no. of unique values`. Ordering may not be preserved as ordering is irrelevant to this data type.
"""
function coerce_categorical(data)
  max_i = size(data, 1)
  max_j = size(data, 2)
  out = Matrix{Int}(undef, max_i, max_j)
  for j in 1:max_j
    u = unique(data[:, j])
    for i in 1:max_i
      out[i, j] = Int(findall(x -> x == data[i, j], u)[1])
    end
  end
  return out
end
